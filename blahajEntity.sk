# Preloads
on load:
	set {blahajScript::blahajHead} to player head named "&bB&dl&fåh&ba&dj &fFace Mask" with nbt from "{SkullOwner:{Id:[I;351810120,-1305655747,-1771231471,122348655],Properties:{textures:[{Value:""eyJ0ZXh0dXJlcyI6eyJTS0lOIjp7InVybCI6Imh0dHA6Ly90ZXh0dXJlcy5taW5lY3JhZnQubmV0L3RleHR1cmUvNjZkNWRhNzlkNzhiZTllNzY2MmY2NWMyNTBhMTdmYzE4ZDBlZTYzMTEwZmFjMDY4ZDUxY2E1ZDU5MmY3MDUyZSJ9fX0=""}]}}}"
	set {blahajScript::blahajFur} to light blue wool named "&fBlahaj Fur"


# Functions
function transformToBlahaj(oEntity: entity):

	# oEntity=(Original entity), dEntity=(Display entity/armor stand)
	
	# Origin entity's apperance
	make {_oEntity} invisible
	make {_oEntity} silent

	# Initialize display entity
	spawn armor stand above {_oEntity}
	set {_dEntity} to last spawned armor stand

	# Link Entities
	set {blahajScript::blahaj::%{_oEntity}'s uuid%::linkedEntity} to {_dEntity}
	set {blahajScript::blahaj::%{_dEntity}'s uuid%::linkedEntity} to {_oEntity}
	
	# Display entity's apperance
	set name of {_dEntity} to "&bB&dl&fåh&ba&dj"
	add nbt from "{CustomNameVisible:1}" to nbt of {_dEntity}
	make {_dEntity} invisible
	set helmet of {_dEntity} to {blahajScript::blahajHead}

	blahajEntityLoop({_oEntity}, {_dEntity})



function blahajEntityLoop(oEntity: entity, dEntity: entity):
	while {_oEntity} is alive:
		teleport {_dEntity} at location 2 blocks below {_oEntity}'s head
		play 1 dust at location 0.5 blocks below {_dEntity}'s head
		wait 1 tick



function checkLinkedEntity(en: entity) :: boolean:
	if {blahajScript::blahaj::%{_en}'s uuid%::linkedEntity} is set:
		return true
	else:
		return false



function shearBlahaj(oEntity: entity):
	set {_dEntity} to {blahajScript::blahaj::%{_oEntity}'s uuid%::linkedEntity}
	set name of {_dEntity} to "&7Blahaj &c✂"
	wait 2 ticks
	loop all dropped items in radius 1 around location 1 blocks above {_oEntity}:
		if "%loop-entity%" contains "wool":
			kill loop-entity
			add 1 to {_counter}
			if {_counter} >= 2:
				exit loop
	drop {blahajScript::blahajFur} at {_oEntity}



function killBlahaj(oEntity: entity, attacker: entity):
	set {_dEntity} to {blahajScript::blahaj::%{_oEntity}'s uuid%::linkedEntity}
	play sound "minecraft:entity.dolphin.death" in friendly creatures category with volume 1 at location of {_oEntity} for all players
	make 20 of dripping water at location 1.2 block above {_dEntity} with offset vector(0.3, 0.5, 0.3)
	wait 20 ticks
	make 20 of poof at location 1.2 block above {_dEntity} with offset vector(0.2, 0.2, 0.2) with extra 0.05
	kill {_dEntity}
	clear {blahajScript::blahaj::%{_oEntity}'s uuid%::linkedEntity}
	clear {blahajScript::blahaj::%{_dEntity}'s uuid%::linkedEntity}



# Events
on spawn of sheep:
	if spawn reason is spawn egg:
		if nearest player is holding sheep spawn egg named "&bB&dl&fåh&ba&dj &fspawn egg":
			transformToBlahaj(event-entity)
	else:
		chance of 40%:
			transformToBlahaj(event-entity)

on death of sheep:
	if checkLinkedEntity(victim) is true:
		cancel drops
		set {_oEntity} to victim
		killBlahaj({_oEntity}, attacker)

on damage of sheep:
	if checkLinkedEntity(victim) is true:
		if victim's health - damage >= 0:
			play sound "minecraft:entity.dolphin.hurt" in friendly creatures category with volume 1 at location of victim for all players

on shear entity:
	if checkLinkedEntity(event-entity) is true:
		set {_oEntity} to event-entity
		shearBlahaj({_oEntity})
		
on sheep grow wool:
	if checkLinkedEntity(event-entity) is true:
		set {_dEntity} to {blahajScript::blahaj::%event-entity's uuid%::linkedEntity}
		set name of {_dEntity} to "&7&bB&dl&fåh&ba&dj"



# Dev Commands
command /getBlahajEgg:
	permission: op
	trigger:
		give sheep egg named "&bB&dl&fåh&ba&dj &fspawn egg" to player